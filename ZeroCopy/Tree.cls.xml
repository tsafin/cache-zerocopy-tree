<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="ZeroCopy.Tree">
<Description>
Experimental class to support connected tree forest</Description>
<IncludeCode>ZeroCopy.Defaults</IncludeCode>
<Super>%RegisteredObject</Super>
<TimeCreated>64169,63548.890846</TimeCreated>

<Property name="TempGlo">
<Description>
By default uses @TempGlo PPG storage or the whole forest (i.e. it's redirected to CACHETEMP)
You could use any local or global for those purposes</Description>
<Type>%String</Type>
<InitialExpression>$$$DOCTREEDEFAULT</InitialExpression>
</Property>

<Property name="DefaultValue">
<Description>
Property allows to reefine default value for undefined tree nodes
Empty string is default, but you are free to change it</Description>
<Type>%String</Type>
</Property>

<Property name="AutoCleanup">
<Description>
Cleanup all created trees upon object closure</Description>
<Type>%Boolean</Type>
<InitialExpression>$$$OK</InitialExpression>
</Property>

<Property name="KnownTrees">
<Description>
Root pointers for all known so far named trees,
i.e. for $this.DocumentContent it would be i%KnownTrees("DocumentContent") pointing to 
the ..TempGlo subtree </Description>
<Type>%String</Type>
<MultiDimensional>1</MultiDimensional>
<Private>1</Private>
</Property>

<Property name="SubsTLB">
<Description>
cache of recently translated/walked subscripts
might point to differennt global altogether
TODO - limit cache size to N recentmost walks</Description>
<Type>%String</Type>
<MultiDimensional>1</MultiDimensional>
<Private>1</Private>
</Property>

<Property name="KeepObjects">
<Description>
we need to keep opened [and their storage retained]
all zerocopy objects we were merged with. 
So keep object handy, increasing their reference counters</Description>
<Type>%String</Type>
<MultiDimensional>1</MultiDimensional>
<Private>1</Private>
</Property>

<Method name="%OnClose">
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	if ..AutoCleanup {
		#dim trees As %String = $order(..KnownTrees(""))
		while trees '= "" {
			set next = $order(..KnownTrees(trees))
			kill @..CalcTreeGloRef(..KnownTrees(trees))
			set trees = next
		}
		// NB! ..KeepObjects(*) array will be cleaned up automatically
		// and their objects temp trees will be removed upon all reference closed
	}
	return $$$OK
]]></Implementation>
</Method>

<Method name="ReserveTree">
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	#dim gloRef As %String = ..TempGlo
	#dim i As %integer = $Increment(@gloRef)
	
	return $name(@gloRef@(i))
]]></Implementation>
</Method>

<Method name="AddTree">
<FormalSpec>Name:%String</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	// i.e. for DocumentContent we will have root node incremented
	// ^||DocTree = 1
	// and gloref pointer saved to KnownTrees array
	// i%KnownTrees("DocumentContent") => ^||DocTree(1)
	
	#dim gloRef As %String = ..ReserveTree()
	set ..KnownTrees(Name) = gloRef
	return gloRef
]]></Implementation>
</Method>

<Method name="DumpTree">
<Description>
Dump particularly named tree branch</Description>
<FormalSpec>Name:%String</FormalSpec>
<Implementation><![CDATA[
	#dim gloRef As %String = $get(..KnownTrees(Name))
	if gloRef '= "" {
		zwrite @gloRef
	}
]]></Implementation>
</Method>

<Method name="PurgeAllTrees">
<Description>
Kill temporary storage for all existing trees kept in the @TempGlo. Don't use it! </Description>
<ClassMethod>1</ClassMethod>
<FormalSpec>TempGlo:%String=$$$DOCTREEDEFAULT</FormalSpec>
<Implementation><![CDATA[	kill @TempGlo
]]></Implementation>
</Method>

<Method name="RetainObjectsRef">
<Description>
increment reference to the object to make sure it will be kept
around till our own closure</Description>
<FormalSpec>Value:%RegisteredObject</FormalSpec>
<Private>1</Private>
<Implementation><![CDATA[
	#dim stringified As %String = ""_Value
	if '$get(..KeepObjects(stringified)) {
		set ..KeepObjects($increment(..KeepObjects)) = Value 	// incRef over Value object 
		set ..KeepObjects(stringified) = 1						// mark entry in "hash" as known
	}
]]></Implementation>
</Method>

<Method name="KillSubtree">
<Description>
if there are some nodes underneath - just kill them</Description>
<FormalSpec>GloRef:%String</FormalSpec>
<Private>1</Private>
<Implementation><![CDATA[
	if GloRef = "" { quit }
	kill @GloRef
]]></Implementation>
</Method>

<Method name="MakePtr">
<Description>
create special $listbuild(42, value) content to recognize pointers from the
average string or numeric</Description>
<CodeMode>expression</CodeMode>
<FormalSpec>S</FormalSpec>
<Implementation><![CDATA[$listbuild(42,S)
]]></Implementation>
</Method>

<Method name="IsPtr">
<Description>
Check whether the passed string value is our "Pointer" or not
[it should be valid $lb and their 1st entry has to be 42 (don't ask)</Description>
<CodeMode>expression</CodeMode>
<FormalSpec>P</FormalSpec>
<Implementation><![CDATA[$listvalid(P) && ($list(P,1) = 42)
]]></Implementation>
</Method>

<Method name="GetPtr">
<Description>
Extract actual pointer value from their encoded $lb(42,Ptr)</Description>
<CodeMode>expression</CodeMode>
<FormalSpec>P</FormalSpec>
<Implementation><![CDATA[$list(P,2)
]]></Implementation>
</Method>

<Method name="Assign">
<Description>
Assign value (scalar or object) to the multi-dimensional node @GloRef</Description>
<FormalSpec>GloRef:%String,Value:%String</FormalSpec>
<ReturnType>%Status</ReturnType>
<Implementation><![CDATA[
	if '$isobject(Value) {
		set @GloRef = Value
	} else {
		do ..RetainObjectsRef(Value)

		#dim treeName As %String = Value.DocTreeName
		#dim object As DocumentTree = Value.TreeObject

		do ..KillSubtree(GloRef)		
		set @GloRef = ..MakePtr(Value.GloRef)
	}
]]></Implementation>
</Method>

<Method name="GloRef">
<Description>
Factory method returning "smart-pointer" wrapped as 
##class(TreePtr) object</Description>
<FormalSpec>Property:%String,Args...</FormalSpec>
<Implementation><![CDATA[
	#dim ptr As TreePtr = ##class(TreePtr).%New()
	// no extra arguments, access to the root
	set ptr.TreeObject = $this
	set ptr.DocTreeName = Property
	set ptr.GloRef = ..CalcTreeGloRef($get(..KnownTrees(Property)), Args...)

	return ptr
]]></Implementation>
</Method>

<Method name="CalcTreeGloRef">
<Description>
Given arguments as subscripts, the @TreeGloRef as root of storage
return the final global reference (no check for existence), also
pass back the calculated @encodedSubs as binary key</Description>
<FormalSpec>TreeGloRef:%String="",Args...</FormalSpec>
<ReturnType>%String</ReturnType>
<Implementation><![CDATA[
	// nothing to start from, bail out
	if TreeGloRef = "" {
		return ""
	}
	#dim i As %Integer = 1
	#dim len As %integer = $get(Args)
#if 0
	// skip all known indices
	while ..SubsTLB($get(Args(i),-1)) {
		
	}
#endif
	for i=1:1:len {
		#dim sub As %String = $get(Args(i))
		set TreeGloRef = $name(@TreeGloRef@(sub))
		#dim value As %String

		// if we found intermediate data node
		// then check whether it's pointer, and dereference it
		// and stay at the same index
		if $data(@TreeGloRef, value)#2 && ..IsPtr(value) {
			set TreeGloRef = ..GetPtr(value)
		} 
	}

	return TreeGloRef
]]></Implementation>
</Method>

<Method name="GetValue">
<Description>
actual implementation of a getter, given @GloRef reference</Description>
<FormalSpec>GloRef:%String</FormalSpec>
<Implementation><![CDATA[
#define USETLB 1

	if GloRef = "" {
		return ..DefaultValue
	}
	#dim value As %String
	
	// 1. fast-track
	if $data(@GloRef, value)#2 {
		if '..IsPtr(value) {
			return value
		} else {
			set GloRef = ..GetPtr(value)
		}
	}
	// 2. slow track
	
#if 0
	// 2.1. check translation cache filled by prior accesses
#if $$$USETLB
	#dim cachedValue As %String = $get(..SubsTLB(GloRef))
	if cachedValue '= "" {
		return $get(@cachedValue, ..DefaultValue)
	}
#endif
	
	// 2.2. proceed full walk
	// FIXME - we will just ignore namespace if any
	#dim prefixName As %String = $qsubscript(GloRef, 0)
	#dim len As %Integer = $qlength(GloRef)
	#dim i As %Integer
	#dim sub As %String 
	#dim preRef As %String = prefixName
	for i=1:1:len {
		set sub = $qsubscript(GloRef, i)
		set preRef = $name(@preRef@(sub))
		
		// if we found intermediate data node
		// then check whether it's pointer, and dereference it
		// and stay at the same index
		if $data(@preRef, value)#2 && ..IsPtr(value) {
			set preRef = ..GetPtr(value)
		} 
	}
#if $$$USETLB
	// 2.3 save translated reference for later access
	set ..SubsTLB(GloRef) = preRef
#endif
	 
	// we are succeeded only if we get to the end of subscripts
	return $get(@preRef, ..DefaultValue)
#else
	return $get(@GloRef, ..DefaultValue)
#endif
]]></Implementation>
</Method>

<Method name="GetMultiArrayDirect">
<Description>
Getter for unknown multidimensional property (i.e. DocumentContent)
silently invokes tree walker</Description>
<Internal>1</Internal>
<FormalSpec>GloRef:%String,Args...</FormalSpec>
<Implementation><![CDATA[
	if GloRef '= "" {
		set GloRef = ..CalcTreeGloRef(GloRef, Args...)
		return ..GetValue(GloRef)
	} else {
		return ""
	}
]]></Implementation>
</Method>

<Method name="GetMultiArray">
<FormalSpec>Property:%String,Args...</FormalSpec>
<Implementation><![CDATA[
	#dim gloRef As %String = $get(..KnownTrees(Property))

	if gloRef '= "" {
		set gloRef = ..CalcTreeGloRef(gloRef, Args...)
		return ..GetValue(gloRef)
	} else {
		return ""
	}
]]></Implementation>
</Method>

<Method name="%DispatchMethod">
<FormalSpec>Method:%String,Args...</FormalSpec>
<Implementation><![CDATA[	return ..GetMultiArray(Method, Args...)
]]></Implementation>
</Method>

<Method name="SetMultiArrayDirect">
<Description>
Setter for unknown multidimensional property</Description>
<Internal>1</Internal>
<FormalSpec>GloRef:%String,Val,Subs...</FormalSpec>
<Implementation><![CDATA[
	if GloRef '= "" {
		set GloRef = ..CalcTreeGloRef(GloRef, Subs...)
		do ..Assign(GloRef, Val)
	}
]]></Implementation>
</Method>

<Method name="SetMultiArray">
<FormalSpec>Property:%String,Val,Subs...</FormalSpec>
<Implementation><![CDATA[
	#dim gloRef As %String = $get(..KnownTrees(Property))
	if gloRef = "" {
		set gloRef = ..AddTree(Property)
	}
	if gloRef '= "" {
		set gloRef = ..CalcTreeGloRef(gloRef, Subs...)
		do ..Assign(gloRef, Val)
	}
]]></Implementation>
</Method>

<Method name="%DispatchSetMultidimProperty">
<FormalSpec>Property:%String,Val,Subs...</FormalSpec>
<Implementation><![CDATA[	do ..SetMultiArray(Property, Val, Subs...)
]]></Implementation>
</Method>

<Method name="%DispatchSetProperty">
<FormalSpec>Property:%String,Val</FormalSpec>
<Implementation><![CDATA[	do ..SetMultiArray(Property, Val)
]]></Implementation>
</Method>
</Class>
</Export>
